use crate::ast;
use crate::enums::*;

grammar;


pub Stmt: ast::Stmt = {
    AlterCollation,
    AlterObjectDepends
};


Quote = r#"""#;


Identifier = r"(?i)[_a-z]\w*";


Name: String = {
    Quote <s: Identifier> Quote => s.to_string(),
    <s: Identifier> => s.to_string()
};


Qualified: ast::QualifiedName = {
    <schema:(<Name> ".")?> <name:Name> => ast::QualifiedName { schema: schema, name: name.to_string() }
};


AlterCollation: ast::Stmt = {
   "ALTER" "COLLATION" <name:Qualified> "REFRESH" "VERSION" => ast::AlterCollationStmt {
        collation_name: name
    }.into()
};


AlterObjectDepends: ast::Stmt = {
    "ALTER" "INDEX" <index_name:Qualified> "DEPENDS" "ON" "EXTENSION" <ext_name:Name> => ast::AlterObjectDependsStmt {
        object_kind: ObjectKind::Index,
        object: None,
        relation: Some(index_name),
        extension_name: ext_name,
    }.into(),

    "ALTER" "MATERIALIZED" "VIEW" <view_name:Qualified> "DEPENDS" "ON" "EXTENSION" <ext_name:Name> => ast::AlterObjectDependsStmt {
        object_kind: ObjectKind::MatView,
        object: None,
        relation: Some(view_name),
        extension_name: ext_name,
    }.into(),

    "ALTER" "TRIGGER" <trig_name:Name> "ON" <table_name:Qualified> "DEPENDS" "ON" "EXTENSION" <ext_name:Name> => ast::AlterObjectDependsStmt {
        object_kind: ObjectKind::Trigger,
        object: Some(ast::QualifiedName { schema: None, name: trig_name.into() }.into()),
        relation: Some(table_name),
        extension_name: ext_name,
    }.into(),
};
